config("utility_public_config") {
  include_dirs = [ "../include" ]
}

source_set("meta") {
  sources = []

  inputs = [
    "../include/xl/scope_exit",
    "../include/xl/reflect",
  ]

  public_configs = [ ":utility_public_config" ]
}

source_set("string") {
  sources = []
  if (is_win) {
    sources += [ "encoding_win.cc" ]
  } else {
    sources += [ "encoding_posix.cc" ]
  }

  inputs = [
    "../include/xl/encoding",
    "../include/xl/native_string",
    "../include/xl/string",
  ]

  cflags = []
  if (is_posix) {
    cflags += [ "-Wno-unused-result" ]
  }

  public_configs = [ ":utility_public_config" ]
}

source_set("crypto") {
  sources = [ "crypto.cc" ]

  inputs = [ "../include/xl/crypto" ]

  cflags = []
  if (is_posix) {
    cflags += [ "-Wno-unused-result" ]
  }

  deps = [ "../thirdparty:libressl_crypto" ]

  public_configs = [ ":utility_public_config" ]
}

source_set("file") {
  sources = [
    "file.cc",
    "zip.h",
  ]
  if (is_win) {
    sources += [
      "unzip_win.cc",
      "zip_win.cc",
    ]
  } else {
    sources += [
      "unzip_posix.cc",
      "zip_posix.cc",
    ]
  }

  inputs = [
    "../include/xl/byte_order.h",
    "../include/xl/file",
    "../include/xl/zip",
  ]

  cflags = []
  if (is_posix) {
    cflags += [ "-Wno-unused-result" ]
  }
  deps = [ "../thirdparty:minizip" ]

  public_configs = [ ":utility_public_config" ]
}

source_set("config") {
  sources = [ "ini.cc" ]

  inputs = [
    "../include/xl/ini",
    "../include/xl/json",
    "../include/xl/xml",
  ]

  cflags = []
  if (is_posix) {
    cflags += [ "-Wno-unused-result" ]
  }

  deps = [
    ":file",
    "../thirdparty:rapidjson",
    "../thirdparty:rapidxml",
  ]

  public_configs = [
    ":utility_public_config",
    "../thirdparty:rapidjson_public_config",
    "../thirdparty:rapidxml_public_config",
  ]
}

source_set("log") {
  sources = [ "log.cc" ]

  inputs = [
    "../include/xl/log",
    "../include/xl/log_init",
  ]

  cflags = []
  if (is_posix) {
    cflags += [ "-Wno-unused-result" ]
  }

  public_configs = [ ":utility_public_config" ]
}

source_set("process") {
  sources = [ "cmdline_options.cc" ]

  inputs = [
    "../include/xl/cmdline_options",
    "../include/xl/process",
  ]

  if (is_win) {
    sources += [ "process_win.cc" ]
  } else {
    sources += [ "process_posix.cc" ]
  }

  cflags = []
  if (is_posix) {
    cflags += [ "-Wno-unused-result" ]
  }

  public_configs = [ ":utility_public_config" ]
}

group("src") {
  deps = [
    ":config",
    ":crypto",
    ":file",
    ":log",
    ":meta",
    ":process",
    ":string",
  ]
}
