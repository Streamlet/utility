import("../../build_options.gni")

source_set("utility") {
  defines = []
  sources = [
    "cmdline_options.cc",
    "cmdline_options.h",
    "crypto.cc",
    "crypto.h",
    "http_client.cc",
    "http_client.h",
    "log.cc",
    "log.h",
    "log_init.h",
    "native_string.h",
    "process_util.h",
    "string_util.h",
  ]

  if (is_win) {
    sources += [
      "encoding.h",
      "encoding_win.cc",
      "process_util_win.cc",
    ]
  } else {
    sources += [ "process_util_posix.cc" ]
  }

  if (is_win && winhttp) {
    sources += [ "http_client_winhttp.cc" ]
  } else {
    sources += [ "http_client_curl.cc" ]
  }

  cflags = []
  if (is_posix) {
    cflags += [ "-Wno-unused-result" ]
  }

  libs = []
  if (is_win) {
    if (winhttp) {
      libs += [ "winhttp.lib" ]  # for http_client_winhttp.cc
    } else {
      # for http_client_curl.cc
      if (is_debug) {
        libs += [ "libcurl-d.lib" ]
      } else {
        libs += [ "libcurl.lib" ]
      }
      libs += [
        "normaliz.lib",
        "wldap32.lib",
        "ws2_32.lib",
      ]
    }
  } else {
    libs += [
      "curl",  # for http_client_curl.cc
    ]
    if (is_linux) {
      libs += [
        "crypto",  # for crypto.cc
      ]
    }
  }

  deps = [
    "../../thirdparty:loki",
    "../../thirdparty:openssl",
  ]
  if (!is_win || !winhttp) {
    deps += [ "../../thirdparty:curl" ]
  }
}

executable("utility_test") {
  testonly = true

  sources = [
    "cmdline_options_test.cc",
    "crypto_test.cc",
    "unittest.cc",
    "url.cc",
    "url.h",
    "url_test.cc",
  ]

  deps = [
    ":utility",
    "../../thirdparty:googletest",
  ]
}

executable("http_client_test") {
  testonly = true
  sources = [ "http_client_test.cc" ]

  deps = [ ":utility" ]
}

executable("cmdline_options_echo") {
  testonly = true
  sources = [ "cmdline_options_echo.cc" ]

  deps = [ ":utility" ]
}

if (is_win) {
  executable("cmdline_options_echo_win") {
    testonly = true
    sources = [ "cmdline_options_echo_win.cc" ]

    deps = [ ":utility" ]
  }
}

executable("process_util_test") {
  testonly = true
  sources = [ "process_util_test.cc" ]

  deps = [ ":utility" ]
}
