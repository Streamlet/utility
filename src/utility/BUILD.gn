declare_args() {
  # On Windows, the default implement is WinHTTP, then cURL
  # On Posix, the default and only implement is cURL
  winhttp = true
}

source_set("utility") {
  configs += [ "//build_config:project_common" ]
  defines = []
  sources = [
    "http_client.cc",
    "http_client.h",
    "url.cc",
    "url.h",
  ]
  if (is_win) {
    if (winhttp) {
      sources += [
        "encoding.h",
        "encoding_win.cc",
        "http_client_winhttp.cc",
      ]
      libs = [ "winhttp.lib" ]
    } else {
      defines += [ "CURL_STATICLIB" ]
      sources += [ "http_client_curl.cc" ]
      libs = [
        "advapi32.lib",
        "crypt32.lib",
        "normaliz.lib",
        "ws2_32.lib",
        "wldap32.lib",
      ]
      if (is_debug) {
        libs += [ "libcurl_a_debug.lib" ]
      } else {
        libs += [ "libcurl_a.lib" ]
      }
    }
  } else {
      defines += [ "CURL_STATICLIB" ]
      sources += [ "http_client_curl.cc" ]
      libs = [ "curl" ]
  }
}

executable("utility_test") {
  configs += [ "//build_config:project_common" ]
  if (is_win) {
    configs += [ "//build/config/win:static_runtime" ]
  }

  sources = [
    "test.cc",
    "url_test.cc",
  ]

  deps = [ ":utility" ]

  if (!is_win) {
    libs = [ "boost_unit_test_framework" ]
  }
}

executable("http_cmd") {
  configs += [ "//build_config:project_common" ]
  if (is_win) {
    configs += [ "//build/config/win:static_runtime" ]
  }

  sources = [ "http_cmd.cc" ]

  deps = [ ":utility" ]
}
