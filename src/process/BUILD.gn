source_set("process") {
  sources = [
    "cmdline_options.cc",
    "cmdline_options.h",
    "process_util.h",
  ]

  if (is_win) {
    sources += [ "process_util_win.cc" ]
  } else {
    sources += [ "process_util_posix.cc" ]
  }

  cflags = []
  if (is_posix) {
    cflags += [ "-Wno-unused-result" ]
  }

  deps = [ "../../thirdparty:loki" ]
}

source_set("unittest") {
  testonly = true
  sources = []
  deps = [
    ":cmdline_options_echo",
    ":process",
    "../../thirdparty:googletest",
  ]
  if (is_win) {
    deps += [ ":cmdline_options_echo_win" ]
  }
}

executable("process_test") {
  testonly = true
  sources = [ "process_util_test.cc" ]
  deps = [ ":process" ]
}

executable("cmdline_options_echo") {
  testonly = true
  sources = [ "cmdline_options_echo.cc" ]
  deps = [ ":process" ]
}

if (is_win) {
  executable("cmdline_options_echo_win") {
    testonly = true
    configs += [ "../../build/config/win:windows_subsystem" ]
    sources = [ "cmdline_options_echo_win.cc" ]
    deps = [ ":process" ]
  }
}
