declare_args() {
  # On Windows, the default implement is WinHTTP, then cURL
  # On Posix, the default and only implement is cURL
  winhttp = true
}

source_set("utility") {
  defines = []
  sources = [
    "crypto.cc",
    "crypto.h",
    "http_client.cc",
    "http_client.h",
    "url.cc",
    "url.h",
  ]
  deps = [
    "../../thirdparty:loki",
    "../../thirdparty:openssl_crypto",
  ]
  if (is_win) {
    libs = []
    if (winhttp) {
      sources += [
        "encoding.h",
        "encoding_win.cc",
        "http_client_winhttp.cc",
      ]
      libs += [ "winhttp.lib" ]
    } else {
      sources += [ "http_client_curl.cc" ]
      libs += [
        "crypt32.lib",
        "normaliz.lib",
        "ws2_32.lib",
        "wldap32.lib",
      ]
      if (is_debug) {
        libs += [ "libcurl-d.lib" ]
      } else {
        libs += [ "libcurl.lib" ]
      }
      deps += [ "../../thirdparty:curl" ]
    }
  } else {
    sources += [ "http_client_curl.cc" ]
    libs = [ "curl" ]
    deps += [ "../../thirdparty:curl" ]
  }
}

executable("utility_test") {
  testonly = true

  sources = [
    "test.cc",
    "url_test.cc",
  ]

  libs = []
  if (is_win) {
    libs += [ "gtest.lib" ]
  } else {
    libs += [ "gtest" ]
  }

  deps = [
    ":utility",
    "../../thirdparty:googletest",
  ]
}

executable("http_client_test") {
  testonly = true
  sources = [ "http_client_test.cc" ]

  deps = [ ":utility" ]
}
