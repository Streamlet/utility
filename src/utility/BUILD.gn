import("../../build_options.gni")

source_set("utility") {
  defines = []
  sources = [
    "crypto.cc",
    "crypto.h",
    "http_client.cc",
    "http_client.h",
    "url.cc",
    "url.h",
  ]
  if (is_win && winhttp) {
    sources += [
      "encoding.h",
      "encoding_win.cc",
      "http_client_winhttp.cc",
    ]
  } else {
    sources += [ "http_client_curl.cc" ]
  }

  libs = []
  if (is_win) {
    libs += [ "crypto-51.lib" ]
    if (winhttp) {
      libs += [ "winhttp.lib" ]
    } else {
      libs += [
        "normaliz.lib",
        "ws2_32.lib",
        "wldap32.lib",
      ]
      if (is_debug) {
        libs += [ "libcurl-d.lib" ]
      } else {
        libs += [ "libcurl.lib" ]
      }
    }
  } else {
    libs += [ "curl" ]
  }

  deps = [
    "../../thirdparty:loki",
    "../../thirdparty:openssl",
  ]
  if (!is_win || !winhttp) {
    deps += [ "../../thirdparty:curl" ]
  }
}

executable("utility_test") {
  testonly = true

  sources = [
    "crypto_test.cc",
    "test.cc",
    "url_test.cc",
  ]

  libs = []
  if (is_win) {
    libs += [ "gtest.lib" ]
  } else {
    libs += [ "gtest" ]
  }

  deps = [
    ":utility",
    "../../thirdparty:googletest",
  ]
}

executable("http_client_test") {
  testonly = true
  sources = [ "http_client_test.cc" ]

  deps = [ ":utility" ]
}
