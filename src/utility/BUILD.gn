source_set("utility") {
  configs += [ "//build_config:project_common" ]
  sources = [
    "url.cc",
    "url.h",
  ]
  if (is_win) {
    sources += [
      "encoding.h",
      "encoding_win.cc",
      "http_client.h",
      "http_client_win.cc",
    ]
    libs = [
      "winhttp.lib",
    ]
  } else {
    sources += [
      "http_client.h",
      "http_client_posix.cc",
    ]
    libs = [
      "ssl",
      "crypto",
    ]
  }
}

executable("utility_test") {
  configs += [ "//build_config:project_common" ]
  if (is_win) {
    configs += [ "//build/config/win:static_runtime" ]
  }

  sources = [
    "test.cc",
    "url_test.cc",
  ]

  deps = [ ":utility" ]

  if (!is_win) {
    libs = [ "boost_unit_test_framework" ]
  }
}

executable("http_cmd") {
  configs += [ "//build_config:project_common" ]
  if (is_win) {
    configs += [ "//build/config/win:static_runtime" ]
  }

  sources = [ "http_cmd.cc" ]

  deps = [ ":utility" ]
}
